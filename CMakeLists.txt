CMAKE_MINIMUM_REQUIRED(VERSION 3.0)
PROJECT(UDS)

SET(UDS_MAJOR_VERSION 0)
SET(UDS_MINOR_VERSION 1)
SET(UDS_PATCH_VERSION 0)
SET(UDS_VERSION ${UDS_MAJOR_VERSION}.${UDS_MINOR_VERSION}.${UDS_PATCH_VERSION})

FIND_PACKAGE(J2534 REQUIRED)

SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11")
SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -pedantic -Wextra -Werror")

#UDS
SET(UDS_SOURCE_FILES ${UDS_SOURCE_FILES} iso14229.h)
SET(UDS_SOURCE_FILES ${UDS_SOURCE_FILES} uds.h uds.cpp)
SET(UDS_SOURCE_FILES ${UDS_SOURCE_FILES} uds_j2534.h uds_j2534.cpp)

SET(LIB_TYPE SHARED)
ADD_LIBRARY(uds ${LIB_TYPE} ${UDS_SOURCE_FILES})
TARGET_LINK_LIBRARIES(uds j2534)
SET_TARGET_PROPERTIES(uds PROPERTIES COMPILE_FLAGS "-DUDS_EXPORTS -DUDS_J2534_EXPORTS")
GET_TARGET_PROPERTY(J2534_INCLUDE_DIRECTORY j2534 INTERFACE_INCLUDE_DIRECTORIES)

# SWIG
FIND_PACKAGE(SWIG REQUIRED)
FIND_PACKAGE(PythonLibs REQUIRED)
SET(CMAKE_SWIG_FLAGS -I${J2534_SWIG_DIRECTORY} -I${PYTHON_INCLUDE_PATH} -I${J2534_INCLUDE_DIRECTORY})

# SWIG UDS
INCLUDE(${SWIG_USE_FILE})
SET_SOURCE_FILES_PROPERTIES(uds.i PROPERTIES CPLUSPLUS ON)
SWIG_ADD_MODULE(uds_python python uds.i)
SWIG_LINK_LIBRARIES(uds_python uds)
SWIG_LINK_LIBRARIES(uds_python ${PYTHON_LIBRARIES})

TARGET_INCLUDE_DIRECTORIES(${SWIG_MODULE_uds_python_REAL_NAME} PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})
TARGET_INCLUDE_DIRECTORIES(${SWIG_MODULE_uds_python_REAL_NAME} PUBLIC ${PYTHON_INCLUDE_PATH})
TARGET_INCLUDE_DIRECTORIES(${SWIG_MODULE_uds_python_REAL_NAME} PUBLIC ${J2534_INCLUDE_DIRECTORY})
SET_TARGET_PROPERTIES(${SWIG_MODULE_uds_python_REAL_NAME} PROPERTIES OUTPUT_NAME "uds")
SET_TARGET_PROPERTIES(${SWIG_MODULE_uds_python_REAL_NAME} PROPERTIES PREFIX "_")
SET_TARGET_PROPERTIES(${SWIG_MODULE_uds_python_REAL_NAME} PROPERTIES COMPILE_FLAGS "-Wno-error=missing-field-initializers")

#
# Install
#

INCLUDE(CMakePackageConfigHelpers)
WRITE_BASIC_PACKAGE_VERSION_FILE("${CMAKE_CURRENT_BINARY_DIR}/UDS/UDSConfigVersion.cmake" VERSION ${UDS_VERSION} COMPATIBILITY AnyNewerVersion)

# Install swig stuff
SET(SwigPackageLocation lib/swig/UDS)
INSTALL(FILES uds.i DESTINATION ${SwigPackageLocation})

# Install python stuffs
EXECUTE_PROCESS(COMMAND python -c "from distutils.sysconfig import get_python_lib; print get_python_lib()" OUTPUT_VARIABLE PYTHON_SITE_PACKAGES OUTPUT_STRIP_TRAILING_WHITESPACE)
INSTALL(TARGETS ${SWIG_MODULE_uds_python_REAL_NAME} DESTINATION ${PYTHON_SITE_PACKAGES})
INSTALL(FILES ${CMAKE_BINARY_DIR}/uds.py DESTINATION ${PYTHON_SITE_PACKAGES})

# Install library stuffs
INSTALL(TARGETS uds EXPORT uds_targets LIBRARY DESTINATION lib ARCHIVE DESTINATION lib RUNTIME DESTINATION bin  INCLUDES DESTINATION include)
INSTALL(FILES uds.h uds_j2534.h iso14229.h DESTINATION include)

# Install cmake stuffs
EXPORT(EXPORT uds_targets FILE "${CMAKE_CURRENT_BINARY_DIR}/UDSTargets.cmake")
SET(ConfigPackageLocation lib/cmake/UDS)
INSTALL(EXPORT uds_targets FILE UDSTargets.cmake DESTINATION ${ConfigPackageLocation})
INSTALL(FILES cmake/UDSConfig.cmake "${CMAKE_CURRENT_BINARY_DIR}/UDS/UDSConfigVersion.cmake" DESTINATION ${ConfigPackageLocation})
